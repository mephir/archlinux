# Maintainer: Pawel Wilk <pwilkmielno@gmail.com>

pkgname=box2-project
pkgver=2.7.4
pkgrel=1
pkgdesc="The Box application simplifies the Phar building process. Out of the box (no pun intended)."
arch=('any')
license=('MIT')
url='https://box-project.github.io/box2/'
depends=('php')
makedepends=('composer' 'git')
source=("${pkgname}::git+https://github.com/box-project/box2.git#tag=${pkgver}"
        "${pkgname}-${pkgver}.phar::https://github.com/box-project/box2/releases/download/${pkgver}/box-${pkgver}.phar")
md5sums=('SKIP'
         'e528f0c8922769c21ba05d535f1aa40e')

prepare() {
  cd ${srcdir}/${pkgname}

  composer install --no-dev --prefer-dist
}

check() {
  cd ${srcdir}/${pkgname}

  # Install the dev dependencies to run the unit tests
  composer -n update --prefer-dist
  bin/phpunit
}

package() {
  install -Dm644 ${srcdir}/${pkgname}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -Dm755 ${pkgname}-${pkgver}.phar ${pkgdir}/usr/bin/box
}
